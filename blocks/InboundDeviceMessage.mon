package apamax.analyticsbuilder.samples;
using apama.analyticsbuilder.BlockBase;
using com.apama.cumulocity.TenantDetails;
using com.softwareag.connectivity.ExtraFieldsDict;
using com.softwareag.connectivity.Chain;
using com.softwareag.connectivity.ConnectivityPlugins;
using com.apama.exceptions.Exception;
using com.apama.cumulocity.devices.DeviceService;
using com.apama.cumulocity.devices.DeviceConsumer;
using com.apama.cumulocity.devices.DeviceMessage;
using apama.analyticsbuilder.InputParams;
using apama.analyticsbuilder.InputHandler;
using apama.analyticsbuilder.Value;
using apama.analyticsbuilder.Activation;

event DeviceInput_$Parameters {
	string transport;
	string topic;
	action $validate() {
		BlockBase.throwsOnEmpty(topic, "topic", self);
		BlockBase.throwsOnEmpty(transport, "transport", self);
	}
}

/** Device input message.

  Receiving from inbound transports such as MQTT Service.

  The output is a base64-encoded string of the device payload. The properties contain the following keys: topic, clientID, transportProperties and transport.

  @$blockCategory Input
*/
event DeviceInput
{
	BlockBase $base;
	DeviceInput_$Parameters $parameters;
	InputHandler inputHandler;
	constant string $OUTPUT_TYPE_value := "pulse";
	action<Activation,Value> $setOutput_value;

	action $validate() {
		InputParams inputParams := InputParams.forEventType(DeviceMessage.getName()).withPartition("clientID");
		inputHandler := $base.consumesInput(inputParams);
	}
	action $timerTriggered(Activation $activation, any $payload)
	{
		DeviceMessage e := <DeviceMessage> $payload;
		Value v := new Value;
		v.value := e.payloadBase64;
		v.properties := {
			"topic": e.topic,
			"clientID": e.clientID,
			"transportProperties": e.transportProperties,
			"transport": $parameters.transport
		};
		v.timestamp := $activation.timestamp;
		$setOutput_value($activation, v);
	}

	action $init()
	{
		sequence<DeviceConsumer> consumers := DeviceService.connectConsumers($base.getTenantDetails().tenantId, $parameters.transport);
		DeviceConsumer d;
		for d in consumers
		{
			monitor.subscribe(d.getSubscribeChannel());
		}
		on all DeviceMessage(topic=$parameters.topic, transportID=$parameters.transport) as msg
		{
			inputHandler.scheduleNow(msg, msg.clientID);
		}
	}
}
