/*
 * Copyright (c) 2013-present Cumulocity GmbH, Duesseldorf, Germany and/or its affiliates and/or their licensors.
 * Use, reproduction, transfer, publication or disclosure is prohibited except as specifically provided for in your License Agreement with Cumulocity GmbH
 */
 
package apamax.analyticsbuilder.samples;

using apama.analyticsbuilder.BlockBase;
using apama.analyticsbuilder.Activation;
using apama.analyticsbuilder.Value;

/**
 * Parameters for Python block.
 */
event Python_$Parameters
{
	import "PythonBlockPlugin" as plugin;
	/**
	 * Python expression
	 * @$semanticType pab_codeEditor_python
	 */
	string expression;
	/** 
	 * This action is called by the framework to validate the parameters of the block. 
	 */
	action $validate() {
		plugin.validate(expression);
	}
}

/**
 * State for the Python block.
 */
event Python_$State
{
	chunk python_state;
}

/**
 * Python
 *
 * Executes python code on the inputs and produces an output.
 *
 * Python code has the following local variables available:
 * - <tt>inputs</tt> - A list of five Value objects, with members value, properties and timestamp corresponding to the block inputs.
 * - <tt>outputs</tt> - A list of five Value objects, with members value, properties and timestamp that can be assigned to for the block outputs.
 * - <tt>state</tt> - A dict() that can be used to store state between activations of the block.
 * - <tt>logger</tt> - A logger object that can be used to log messages.
 * - <tt>generate</tt> - A boolean which defaults to true, but can be set to false to prevent the block from generating output.
 * For example:
 * <pre>
state["counter"] = state.get("counter", 0.) + 1.
if inputs[0].value and inputs[1].value:
	outputs[0].value = abs(inputs[0].value - inputs[1].value)
	outputs[1].value = inputs[0].value - inputs[1].value
	outputs[3].value = state["counter"]
	outputs[2].properties = {'value1': inputs[0].properties['value1'], 'value2': inputs[1].properties['value2']}
else:
	generate = False
 * </pre>
 *
 * @$blockCategory Calculations
 */
event Python 
{
	import "PythonBlockPlugin" as plugin;
	
	/**BlockBase object.
	 *
	 * This is initialized by the framework when the block is required for a model.
	 */
	BlockBase $base;
	
	/** Block parameters, filled in by the framework. */
	Python_$Parameters $parameters;

	/** Setup the return vector */
	action $init()
	{
		setOutput := [$setOutput_result1, $setOutput_result2, $setOutput_result3, $setOutput_result4, $setOutput_result5];
		compiled_expression := plugin.validate($parameters.expression);
	}

	/**
	 * This action receives the input values and contains the logic of the block. 
	 *
	 * @param $activation The current activation, contextual information required when generating a block output. Blocks should only use the
	 * <tt>Activation</tt> object passed to them from the framework, never creating their own or holding on to an <tt>Activation</tt> object.
	 * @param $input_value1 First input to the block.
	 * @param $input_value2 Second input to the block.
	 * @param $input_value3 Third input to the block.
	 * @param $input_value4 Fourth input to the block.
	 * @param $input_value5 Fifth input to the block.
	 *
	 * @$inputName value1 input0
	 * @$inputName value2 input1
	 * @$inputName value3 input2
	 * @$inputName value4 input3
	 * @$inputName value5 input4
	 */
	action $process(Activation $activation, Value $input_value1, Value $input_value2, Value $input_value3, Value $input_value4, Value $input_value5, Python_$State $blockState)
	{
		if $blockState.python_state.empty() {
			// If the python state is not initialized, we initialize it.
			$blockState.python_state := plugin.createPythonState();
		}
		sequence<Value> results := plugin.execute(compiled_expression, $blockState.python_state, [$input_value1, $input_value2, $input_value3, $input_value4, $input_value5]);

		if results.size()>0 and <boolean> results[0].value {
			integer i := 1;
			while i < results.size() and i <= setOutput.size() {
				setOutput[i-1]($activation, results[i]);
				i := i + 1;
			}
		}
	}
	
	/**
	 * output0
	 */
	action<Activation,Value> $setOutput_result1;		// This is initialized by the framework. It sets the output of the block and may trigger any blocks connected to this output.
	
	/**
	 * output1
	 */
	action<Activation,Value> $setOutput_result2;	// This is initialized by the framework. It sets the output of the block and may trigger any blocks connected to this output.
	
	/**
	 * output2
	 */
	action<Activation,Value> $setOutput_result3;	// This is initialized by the framework. It sets the output of the block and may trigger any blocks connected to this output.
	
	/**
	 * output3
	 */
	action<Activation,Value> $setOutput_result4;	// This is initialized by the framework. It sets the output of the block and may trigger any blocks connected to this output.
	
	/**
	 * output4
	 */
	action<Activation,Value> $setOutput_result5;	// This is initialized by the framework. It sets the output of the block and may trigger any blocks connected to this output.

	sequence<action<Activation,Value>> setOutput;
	chunk compiled_expression;
}
